#! /usr/bin/perl

use lib qw( /usr/local/psf/lib );
use Mojolicious::Lite;
use Mojo::IOLoop::Delay;
use Try::Tiny;
use PSF::Server;
use PHP::Session;
use JSON::XS;
use Digest::SHA1 qw( sha1_hex );
use List::MoreUtils (qw( first_index ));
use MIME::Base64 qw( encode_base64 decode_base64 );
use Data::Dumper;
use Clone qw( clone );
use EV;

our $json     = (new JSON::XS())->boolean_values( 0, 1 );
our $server   = new PSF::Server();
our $DEBUG    = 0;

# ------------------------------------------------------------
# MANAGE RINGS, DIVISIONS, AND ATHLETES VIA WEBSOCKETS
# ------------------------------------------------------------
websocket '/:ring/:role' => sub {
	my $self    = shift;
	my $rnum    = $self->param( 'ring' );
	my $client  = $server->registry->add( $self, $rnum );
	my $ring    = undef;

	$self->inactivity_timeout( 3600 ); # 1 hour
	$client->ping->start();

	# ----------------------------------------
	# Handle messages
	# ----------------------------------------
	$self->on( message => sub {
		my $self    = shift;
		my $request = $json->decode( shift );

		# ===== REJECT UNAUTHORIZED USERS
		my $security   = $server->config->security();
		if( $security->enabled() ) {
			unless( $security->authenticated( $client ) && $security->authorized( $rnum )) {
				$server->send->client( { json => { error => $security->unauthorized(), request => $request }});
				return;
			}
		}

		$request->{ ring } = $rnum;
		$request->{ from } = $client->cid();

		# ===== HANDLE REQUEST
		try { 
			$server->handle( $request ); 

		} catch { 
			$server->send->client({ json => { error => "Error while processing request: $_\n", request => $request }}); 

		};
		print STDERR "\n" if $DEBUG;
	});

	# ----------------------------------------
	# Handle disconnects
	# ----------------------------------------
	$self->on( finish => sub { 
		my $request = { type => 'user', action => 'update', ring => $rnum, user => { action => 'disconnect', sessid => $client->sessid(), id => $client->id(), role => $client->role() }};
		$server->registry->remove( $client );
		$server->broadcast_updated_users( $request, $ring, $client->group());
		$client->ping->quit();
	});
};

# ============================================================
# STATUS
# ============================================================
get '/status' => sub {
	my $self = shift;
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $server->config->host() );
	$self->render( text => 'OK' );
};

# ============================================================
# ERROR TRAPPING
# ============================================================
any '*command' => sub {
	my $self = shift;
	my $command = $self->param( 'command' );
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $server->config->host() );
	$self->render( json => { error => "PSF Service Error: Unknown command: $command" });
};

# ============================================================
# HYPNOTOAD SERVER
# ============================================================
app->config( hypnotoad => { listen => [ 'http://*:3030' ], pid_file => '/var/run/psf.pid', workers => 1 });
app->log( new Mojo::Log( path => '/var/log/psf.log', level => 'debug' ));
app->start();
